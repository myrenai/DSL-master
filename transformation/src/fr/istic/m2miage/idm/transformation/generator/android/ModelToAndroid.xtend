package fr.istic.m2miage.idm.transformation.generator.android

import fr.istic.m2miage.idm.cSS.Style
import fr.istic.m2miage.idm.uimm.Component
import fr.istic.m2miage.idm.uimm.Element
import fr.istic.m2miage.idm.uimm.ImageComponent
import fr.istic.m2miage.idm.uimm.ListComponent
import fr.istic.m2miage.idm.uimm.Option
import fr.istic.m2miage.idm.uimm.OptionType
import fr.istic.m2miage.idm.uimm.Page
import fr.istic.m2miage.idm.uimm.TextComponent
import org.eclipse.emf.common.util.EList

class ModelToAndroid {
	var Page p;
	new(Page page){
		this.p = page;
	}
	
	def createJavaClass(String packageName)'''
package «packageName»;

import android.content.Context;
import android.widget.Button;
import android.widget.CheckBox;
import android.widget.EditText;
import android.widget.ImageView;
import android.widget.RadioGroup;
import android.widget.ScrollView;
import android.widget.TableLayout;

/**
	Generated by ModelToAndroid generator
*/
public class «p.title» extends ScrollView{
	
	private Context ctx;
	private TableLayout tableLayout;
	public «p.title»(Context context) {
		super(context);
		this.ctx = context;
		
		tableLayout = new TableLayout(ctx);
		addView(tableLayout);
		
		// Poll System Title
		PollSystemUtils.addTextView(ctx, tableLayout, "«p.title»");
		
		«FOR poll : p.components »
		«printPoll(poll)»
		«ENDFOR»
		
		Button btn = PollSystemUtils.addButton(ctx, tableLayout, "Save");
	}
}
	'''
	
	def printPoll(Component poll) '''
	// Poll title
	PollSystemUtils.addTextView(ctx, tableLayout, "«poll.description»");
	«FOR question : poll.components»
	«printQuestion(question)»
	«ENDFOR»
	'''
	
	def printQuestion(Component q) {
		if(q instanceof ImageComponent){
			printImageComponent(q)
		} else if(q instanceof ListComponent){
			printListComponent(q)
		} else if(q instanceof TextComponent){
			printTextComponent(q)
		}
	}
	
	def printTextComponent(TextComponent q)'''
	
	// Text question
	PollSystemUtils.addTextView(ctx, tableLayout, "«q.text»");
	EditText «q.name» = PollSystemUtils.addTextField(ctx, tableLayout);
	'''
	
	def printImageComponent(ImageComponent q)'''
	
	// Image question
	PollSystemUtils.addTextView(ctx, tableLayout, "«q.text»");
	ImageView iv«q.name» = PollSystemUtils.addImageView(ctx, tableLayout, R.drawable.«q.src»);// TODO
	EditText «q.name» = PollSystemUtils.addTextField(ctx, tableLayout);
	'''
	
	def printListComponent(ListComponent q)'''
	
	// List question
	PollSystemUtils.addTextView(ctx, tableLayout, "«q.text»");
	«printOption(q.elements)»
	'''
	
	def printOption(EList<Element> list) {
		if(list != null){
			var Option o = list.get(0) as Option
			if(o.type == OptionType.CHECK_BOX){
				return printCheckBox(list);
			}else{
				return printRadioButton(list);
			}
		}
	}
	
	def printRadioButton(EList<Element> list) '''
	RadioGroup rg1 = PollSystemUtils.addRadioGroup(ctx, tableLayout«printOptionParams(list)»);
	'''
	def printCheckBox(EList<Element> list) '''
	CheckBox[] cb = PollSystemUtils.addCheckBox(ctx, tableLayout«printOptionParams(list)»);	
	'''
		
	def printOptionParams(EList<Element> list) {
		var String option = "";
		for(Element o : list){
			option += ", \""+ (o as Option).text+ "\"";
		}
		return option;
	}

}